plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "irisflw.mixins.flw.json"
        mixinConfig "irisflw.mixins.iris.json"
        mixinConfig "irisflw.mixins.forge.json"
    }
    runs {
        client {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
        }
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven { url = "https://mvn.devos.one/snapshots" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modCompileOnly ("dev.engine-room.flywheel:flywheel-forge-${flywheel_minecraft_version}:${flywheel_forge_version}")
    modCompileOnly ("maven.modrinth:oculus:${oculus_version}-forge")

    forgeRuntimeLibrary ('org.anarres:jcpp:1.4.14')
//    localRuntime 'io.github.douira:glsl-transformer:2.0.1'
    modLocalRuntime ("maven.modrinth:oculus:${oculus_version}-forge")
    modLocalRuntime ("maven.modrinth:embeddium:${embeddium_version}-forge")
    modLocalRuntime ("com.simibubi.create:create-${minecraft_version}:${create_version}") { transitive = false }
    modLocalRuntime ("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}")
//    modLocalRuntime ("dev.engine-room.flywheel:flywheel-neoforge-${flywheel_minecraft_version}:${flywheel_neoforge_version}")
    modLocalRuntime("com.tterrag.registrate:Registrate:${registrate_version}")
    modLocalRuntime ("dev.engine-room.vanillin:vanillin-forge-${flywheel_minecraft_version}:${vanillin_forge_version}")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
